apply plugin: 'com.android.library'


static def getGitVersion() {
    try {
        return 'git rev-parse --short HEAD'.execute().text.trim()
    } catch (Throwable ignored) {
        return ""
    }
}

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode 44
        versionName '4.4.0'
        buildConfigField "String", "GIT_REVISION", "\"${getGitVersion()}\""
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['libs']
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

project.afterEvaluate {
    tasks.all { task ->
        if (task.name =~ /generate.*BuildConfig/) {
            task.dependsOn(propagateBuildConfig)
        }
    }
}

// workaround of gradle bug: build type of library is always release.
task propagateBuildConfig << {
    project.android.buildTypes.all { type ->
        type.debuggable = rootProject.ext.has("debugBuild") ? rootProject.ext.debugBuild : false
    }
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'], exclude: ['android-support-v4.jar'])
    api(rootProject.ext.dependencies.rn) {
        exclude group: 'com.android.support'
    }
    api project(path: ':commonlib')
}